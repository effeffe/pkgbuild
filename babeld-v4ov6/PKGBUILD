# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>

pkgname=babeld-v4ov6
pkgver=1.10
pkgrel=1
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('x86_64')
url="https://www.irif.fr/~jch/software/babel/"
license=('MIT')
depends=('glibc')
provides=('babeld')
conflicts=('babeld')
backup=('etc/babeld.conf')
source=("https://www.irif.fr/~jch/software/files/babeld-$pkgver.tar.gz"
        'babeld.patch' 'babeld.conf' 'babeld.service')
sha512sums=('7b6e78e4695bfd9af314b05cbfad384c3d33ffa8c032860b16b64fe5e09d20616441c49bc67c03c9a48e8af4f9216b6ac2ee6c4a43d2be9553194e8b64e06553'
            'd13c7581b24bb9b88e8281a68f5c1097ab932e3f01c6e2d4315b62d34e80aebacf8d88a7a9c832b427f29a7fadf561e55698c53a586a562c8a61b209e8993795'
            '1232684447b4409d6e3813041f4ab1158faa844b5ac65441b5f299f1e353f48679aacbce9cc79e6714e9a3e1b17d2cfa4963c1388dba225ee342ea3cdb7f1194'
            '01e69d19a6176a6bf26ee703939c5bdea3f1245464ed5d3863dc5867a76b8b6a6fc7324912aee17458a0c6275ccdfa5abe63b88f7503c8d4dfd37d3f15f42700')

prepare() {
  cd "babeld-$pkgver"
  patch -p1 < ../babeld.patch
}

build() {
  cd "babeld-$pkgver"
  make PREFIX="/usr" PLATFORM_DEFINES="${CFLAGS}" CDEBUGFLAGS=""
}

package() {
  cd "babeld-$pkgver"
  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "$pkgdir"/etc/babeld.conf
  install -Dm644 ../babeld.service "$pkgdir"/usr/lib/systemd/system/babeld.service
  install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
